// Библиотека проектных подсистем для упрощения разработки архитектуры на 1С: Предприятие 8,
// включая доработку типовых конфигураций.
//
// Copyright First BIT company
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//       http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//
// URL:    https://github.com/firstBitSportivnaya/PSSL/
//

#Область ПрограммныйИнтерфейс

// Инициализирует структуру параметров чтения
// 
// Возвращаемое значение:
//  Структура - см. пбп_ЗагрузкаФайлаЧерезТабличныйДокументСервер.ПолучитьПараметрыЧтенияФайла
//
Функция ПолучитьПараметрыЧтенияФайла() Экспорт
	
	СтруктураПараметров = пбп_ЗагрузкаФайлаЧерезТабличныйДокументВызовСервера.ПолучитьПараметрыЧтенияФайла();
	СтруктураПараметров.Вставить("АдресМакета"		, "");
	СтруктураПараметров.Вставить("АдресПомещения"	, "");
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Загружает файл XLS, парсит его и помещает результат в таблицу значений, которая передается во временное хранилище
//
// Параметры:
//  АдресСоответствияКолонок	 - Строка	 - описание колонок загружаемого файла в таблице значений,
//  	помещенное во временное хранилище на Сервере.
//  	см. пбп_ЗагрузкаФайлаЧерезТабличныйДокументВызовСервера.ИнициализироватьТаблицуСоСвойствамиКолонок
//  ПараметрыЧтения				 - Структура - см. ИнициализироватьСтруктуруПараметровЧтения
// 
// Возвращаемое значение:
//  Строка - Адрес файла во временном хранилище
//
Асинх Функция ЗагрузитьИзXLS(АдресСоответствияКолонок, ПараметрыЧтения) Экспорт
	
	АдресПомещения = пбп_ЗагрузкаФайлаЧерезТабличныйДокументВызовСервера.ПоместитьЗаглушку(АдресСоответствияКолонок);
	
	ПараметрыЧтения.АдресМакета		= АдресСоответствияКолонок;
	ПараметрыЧтения.АдресПомещения	= АдресПомещения;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = "Документ Excel (*.xls, *.xlsx)|*.xls;*.xlsx|CSV-файл (*.csv)|*.csv";
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.ИндексФильтра = 0;
	ПараметрыДиалога.Заголовок = "Выберите файл для загрузки";
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх( , , , ПараметрыДиалога);
	
	Если ТипЗнч(ОписаниеФайла) = Тип("ОписаниеПомещенногоФайла") И Не ОписаниеФайла.ПомещениеФайлаОтменено Тогда
		пбп_ЗагрузкаФайлаЧерезТабличныйДокументВызовСервера.ФормированиеТаблицы(
			ОписаниеФайла.Адрес, ОписаниеФайла.СсылкаНаФайл.Расширение, ПараметрыЧтения);
	Иначе
		пбп_ПереадресацияКлиент.СообщитьПользователю(НСтр("ru = 'Помещение файла отменено'"));
		АдресПомещения = Неопределено;
	КонецЕсли;
	
	Возврат АдресПомещения;
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции



#КонецОбласти // СлужебныеПроцедурыИФункции